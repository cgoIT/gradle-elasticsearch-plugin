buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

group = "cgoit.gradle.elasticsearch"
version = "0.2.3"

String repoName = 'gradle-elasticsearch-plugin'

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

compileGroovy {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "de.undercouch:gradle-download-task:4.0.4"
    compile "org.apache.httpcomponents:fluent-hc:4.5.12"
    compile "com.kstruct:gethostname4j:0.0.3"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'a gradle plugin for starting and stopping ElasticSearch during integration tests')
                root.children().last() + pomConfig
            }
        }
    }
}

task install(dependsOn: 'publishMavenPublicationToMavenLocal') {
    doLast {
        logger.lifecycle "installing $project.name version $version"
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/${project.name}-${project.version}.pom")
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    publications = ['maven']

    publish = true

    pkg {
        repo = 'maven'
        name = project.name
        websiteUrl = "https://github.com/cgoIT/$repoName"
        issueTrackerUrl = "https://github.com/cgoIT/$repoName/issues"
        vcsUrl = "https://github.com/cgoIT/${repoName}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin', 'elastic', 'ElasticSearch', repoName]
        version {
            name = project.version
        }
    }
}

pluginBundle {
    website = "https://github.com/cgoIT/$repoName"
    vcsUrl = "https://github.com/cgoIT/${repoName}.git"
    description = 'An ElasticSearch gradle plugin for starting an ElasticSearch within a gradle build.'
    tags = ['elastic', 'ElasticSearch']

    plugins {
        greetingsPlugin {
            id = project.group
            displayName = 'Gradle ElasticSearch plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

bintrayUpload.dependsOn = [sourcesJar]
publishPlugins.dependsOn = [bintrayUpload]
